buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Deps.gradle
        classpath Deps.kotlin_gradle_plugin
        classpath Deps.versions_gradle_plugin //run dependencyUpdates task to check dependencies are up to date
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions" //for Deps.versions_gradle_plugin
            android {
                buildToolsVersion Config.buildTools
                compileSdkVersion Config.compileSdk
                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                    consumerProguardFiles 'consumer-rules.pro'
                }
                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//run projectDependecyGraph task to generate modules dependencies graph
apply from: file('gradle/projectDependencyGraph.gradle')